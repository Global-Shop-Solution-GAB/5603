Program.Sub.ScreenSU.Start
Gui.F_Message..Create
Gui.F_Message..Caption("WARNING!")
Gui.F_Message..Size(5775,3225)
Gui.F_Message..MinX(0)
Gui.F_Message..MinY(0)
Gui.F_Message..Position(0,0)
Gui.F_Message..BackColor(255)
Gui.F_Message..MousePointer(0)
Gui.F_Message..AlwaysOnTop(True)
Gui.F_Message..ControlBox(False)
Gui.F_Message..MaxButton(False)
Gui.F_Message..Sizeable(False)
Gui.F_Message.txtMessage.Create(TextboxM)
Gui.F_Message.txtMessage.Size(5340,2085)
Gui.F_Message.txtMessage.Position(150,150)
Gui.F_Message.txtMessage.BackColor(255)
Gui.F_Message.txtMessage.ForeColor(16777215)
Gui.F_Message.cmdYes.Create(Button)
Gui.F_Message.cmdYes.Size(855,375)
Gui.F_Message.cmdYes.Position(405,2415)
Gui.F_Message.cmdYes.Caption("Yes")
Gui.F_Message.cmdYes.Event(Click,cmdYes_Click)
Gui.F_Message.cmdYes.ForeColor(16777215)
Gui.F_Message.cmdNo.Create(Button)
Gui.F_Message.cmdNo.Size(855,375)
Gui.F_Message.cmdNo.Position(4350,2415)
Gui.F_Message.cmdNo.Event(Click,cmdNo_Click)
Gui.F_Message.cmdNo.ForeColor(16777215)
Gui.F_Message.cmdNo.Caption("No")
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Author: Bix
'Date: 9 July 2019
'GCG_5603_Freight_and_Custom_check.g2u
'Checks if PO has a freight and customs vendor then if there have been charges for them on the PO
V.Local.sError.Declare
V.Local.sPO.Declare
V.Local.bExists.Declare
F.Intrinsic.Control.Try

F.Intrinsic.Control.SelectCase(V.Caller.Hook)
	F.Intrinsic.Control.Case(15020)
		F.Intrinsic.String.Build("{0}Customs",V.Ambient.NewLine,V.Passed.000205)
	F.Intrinsic.Control.Case(15041)
		F.Intrinsic.String.LPad(V.Passed.000002,"0",7,V.Local.sPO)
		F.Intrinsic.Control.CallSub(CheckFreightAndCustoms,"PO",V.Local.sPO,"FreightVendor",V.Passed.000138,"Freight",V.Passed.000008,"CustomsVendor",V.Passed.000142,"Customs",V.Passed.000145)
		F.Intrinsic.Control.If(V.Args.Check,>,0)
			F.Intrinsic.Control.CallSub(NotifyUser,"Check",V.Args.Check,"FreightVendor",V.Args.DBFreightVendor,"CustomsVendor",V.Args.DBCustomsVendor)
		F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndSelect

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Caller.ScriptFile,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.CheckFreightAndCustoms.Start
'V.Args.PO, V.Args.FreightVendor, V.Args.Freight, V.Args.CustomsVendor, V.Args.Customs
V.Local.sError.Declare
V.Local.sSQL.Declare
V.Local.sFreightVendor.Declare
V.Local.sCustomsVendor.Declare
V.Local.bFreightDBCheck.Declare
V.Local.bFreightVendorCheck.Declare
V.Local.bFreightCheck.Declare
V.Local.bCustomsDBCheck.Declare
V.Local.bCustomsVendorCheck.Declare
V.Local.bCustomsCheck.Declare
V.Local.iChk.Declare
F.Intrinsic.Control.Try

'Get freight and customs vendors from PO header
F.Intrinsic.String.Build("select FREIGHT_VENDOR, CUSTOMS_VENDOR from V_PO_HEADER where PURCHASE_ORDER = '{0}';",V.Args.PO,V.Local.sSQL)
F.ODBC.Connection!con.OpenCompanyConnection
F.ODBC.Connection!con.ExecuteAndReturn(V.Local.sSQL,V.Local.sSQL)
F.ODBC.Connection!con.Close
F.Intrinsic.String.Split(V.Local.sSQL,"*!*",V.Local.sSQL)
V.Local.sFreightVendor.Set(V.Local.sSQL(0))
V.Local.sCustomsVendor.Set(V.Local.sSQL(1))
'Check Freight Values
F.Intrinsic.Control.If(V.Local.sFreightVendor.Trim,!=,"")
	V.Local.bFreightDBCheck.Set(True)
	F.Intrinsic.Control.If(V.Local.sFreightVendor.Trim,=,V.Args.FreightVendor.Trim)
		V.Local.bFreightVendorCheck.Set(True)
	F.Intrinsic.Control.Else
		V.Local.bFreightVendorCheck.Set(False)
	F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.If(V.Args.Freight,!=,0)
		V.Local.bFreightCheck.Set(True)
	F.Intrinsic.Control.Else
		V.Local.bFreightCheck.Set(False)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Else
	V.Local.bFreightDBCheck.Set(False)
F.Intrinsic.Control.EndIf
'Check Customs Values
F.Intrinsic.Control.If(V.Local.sCustomsVendor.Trim,!=,"")
	V.Local.bCustomsDBCheck.Set(True)
	F.Intrinsic.Control.If(V.Local.sCustomsVendor.Trim,=,V.Args.CustomsVendor.Trim)
		V.Local.bCustomsVendorCheck.Set(True)
	F.Intrinsic.Control.Else
		V.Local.bCustomsVendorCheck.Set(False)
	F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.If(V.Args.Customs,!=,0)
		V.Local.bCustomsCheck.Set(True)
	F.Intrinsic.Control.Else
		V.Local.bCustomsCheck.Set(False)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Else
	V.Local.bCustomsDBCheck.Set(False)
F.Intrinsic.Control.EndIf
'Read booleans and assign an ID to output a message
V.Local.iChk.Set(0)
F.Intrinsic.Control.If(V.Local.bFreightDBCheck)
	F.Intrinsic.Control.If(V.Local.bFreightVendorCheck.Not)
		F.Intrinsic.Math.Add(V.Local.iChk,1,V.Local.iChk)
	F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.If(V.Local.bFreightCheck.Not)
		F.Intrinsic.Math.Add(V.Local.iChk,2,V.Local.iChk)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf
F.Intrinsic.Control.If(V.Local.bCustomsDBCheck)
	F.Intrinsic.Control.If(V.Local.bCustomsVendorCheck.Not)
		F.Intrinsic.Math.Add(V.Local.iChk,4,V.Local.iChk)
	F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.If(V.Local.bCustomsCheck.Not)
		F.Intrinsic.Math.Add(V.Local.iChk,8,V.Local.iChk)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf
F.Intrinsic.Variable.AddRV("Check",V.Local.iChk)
F.Intrinsic.Variable.AddRV("DBFreightVendor",V.Local.sFreightVendor)
F.Intrinsic.Variable.AddRV("DBCustomsVendor",V.Local.sCustomsVendor)

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Caller.ScriptFile,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End
F.Intrinsic.Control.EndTry
Program.Sub.CheckFreightAndCustoms.End

Program.Sub.NotifyUser.Start
V.Local.sError.Declare
V.Local.iCheck.Declare
V.Local.sMessage.Declare
V.Local.iRet.Declare
F.Intrinsic.Control.Try

F.Intrinsic.Math.Sub(V.Args.Check,8,V.Local.iCheck)
F.Intrinsic.Control.If(V.Local.iCheck,>=,0)
	F.Intrinsic.String.Build("- The Customs Amount is Zero{0}{0}",V.Ambient.NewLine,V.Local.sMessage)
F.Intrinsic.Control.Else
	F.Intrinsic.Math.Add(V.Local.iCheck,8,V.Local.iCheck)
F.Intrinsic.Control.EndIf
F.Intrinsic.Control.If(V.Local.iCheck,>,0)
	F.Intrinsic.Math.Sub(V.Local.iCheck,4,V.Local.iCheck)
	F.Intrinsic.Control.If(V.Local.iCheck,>=,0)
		F.Intrinsic.String.Build("{2}- The Customs Vendor does not match the Purchase Order's Customs Vendor{0}Customs Vendor in DataBase: {3}{1}{3}{0}{0}",V.Ambient.NewLine,V.Args.CustomsVendor.Trim,V.Local.sMessage,V.Ambient.DblQuote,V.Local.sMessage)
	F.Intrinsic.Control.Else
		F.Intrinsic.Math.Add(V.Local.iCheck,4,V.Local.iCheck)
	F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.If(V.Local.iCheck,>,0)
		F.Intrinsic.Math.Sub(V.Local.iCheck,2,V.Local.iCheck)
		F.Intrinsic.Control.If(V.Local.iCheck,>=,0)
			F.Intrinsic.String.Build("{1}- The Freight Amount is Zero{0}{0}",V.Ambient.NewLine,V.Local.sMessage,V.Local.sMessage)
		F.Intrinsic.Control.Else
			F.Intrinsic.Math.Add(V.Local.iCheck,2,V.Local.iCheck)
		F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.If(V.Local.iCheck,>,0)
			F.Intrinsic.String.Build("{2}- The Freight Vendor does not match the Purchase Order's Freight Vendor{0}Freight Vendor in DataBase: {3}{1}{3}{0}{0}",V.Ambient.NewLine,V.Args.FreightVendor.Trim,V.Local.sMessage,V.Ambient.DblQuote,V.Local.sMessage)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf
F.Intrinsic.String.Build("{0}Would you like to continue?",V.Local.sMessage,V.Local.sMessage)

Gui.F_Message.txtMessage.Text(V.Local.sMessage)
Gui.F_Message..Show

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Caller.ScriptFile,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End
F.Intrinsic.Control.EndTry
Program.Sub.NotifyUser.End

Program.Sub.cmdYes_Click.Start
V.Local.bExists.Declare
F.Intrinsic.Variable.PassedExists("777777",V.Local.bExists)
F.Intrinsic.Control.If(V.Local.bExists)
	V.Passed.777777.Set(0)
F.Intrinsic.Control.EndIf
F.Intrinsic.Control.End
Program.Sub.cmdYes_Click.End

Program.Sub.cmdNo_Click.Start
V.Local.bExists.Declare
F.Intrinsic.Variable.PassedExists("777777",V.Local.bExists)
F.Intrinsic.Control.If(V.Local.bExists)
	V.Passed.777777.Set(1)
F.Intrinsic.Control.EndIf
F.Intrinsic.Control.End
Program.Sub.cmdNo_Click.End
